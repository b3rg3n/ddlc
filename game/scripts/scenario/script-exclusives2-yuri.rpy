label yuri_exclusive2_1:
    scene bg club_day
    with wipeleft_scene
    "Мне было крайне любопытно побольше пообщаться с Юри..."
    "Но в то же время было бы невежливо отвлекать её от чтения."
    "Краем глаза я заметил обложку её книги."
    "Судя по всему, это та же самая, что она одолжила мне..."
    "Более того, она, судя по всему, не ушла дальше первых страниц."
    play music t6 fadeout 1.0
    show yuri 4a zorder 2 at t11
    y "А..."
    "Чёрт..."
    "Кажется, она заметила, что я смотрю на неё..."
    "Юри снова оглянулась на меня, и наши взгляды на долю секунды пересеклись."
    y 4b "..."
    "Но это лишь заставило её ещё сильнее зарыться в свою книгу."
    mc "Извини..."
    mc "Я что-то немного задумался..."
    "Пробормотал я, ощутив, что заставил её чувствовать себя неуютно."
    y 1i "Эм..."
    y "Ничего страшного..."
    y 1a "Если бы я не отвлекалась, то и не заметила бы вообще."
    y "Но я просто перечитывала кое-что и..."
    mc "Это же книга, которую ты дала мне, да?"
    y "Угу."
    y "Я хотела частично перечитать её."
    y 2q "Без какой-то особой на то причины!.."
    mc "Интересно, а как так получилось, что у тебя оказалось две одинаковых книги?"
    y "А..."
    y "Ну, я вчера зашла в книжный..."
    y 3o "То есть, всё было не так..."
    y "То есть..."
    y 1w "Так... получилось, что я купила сразу две."
    mc "А, вот оно что."
    "Юри явно не договаривала о чём-то вполне очевидном, но я решил не донимать её."
    mc "Я обязательно скоро начну её читать!"
    y 2u "Рада слышать..."
    y "Когда события наберут обороты, тебе не захочется от неё отрываться."
    y 2c "Это очень цепляющая история, героям которой хочется сопереживать."
    mc "Вот как?.."
label yuri_exclusive2_1_ch22:
    mc "А о чём она вообще?"
    y 1f "Ну..."
    y "Хм-м..."
    "Юри закрыла книгу и пробежалась глазами по её задней обложке."
    "Книга называлась \"Портрет Маркова\"."
    "На её передней обложке красовался зловещий символ в виде глаза."
    y 1a "В общем она о религиозном лагере, который превратили в тюрьму для экспериментов над людьми..."
    y "И все заключённые имеют общую черту: они превращаются в кровожадные машины убийства."
    y 1m "Но ситуация в комплексе становится ещё хуже, когда там начинают выборочно скрещивать людей, отрезая конечности, а затем пришивая их к..."
    y 1q "О-ой, я тебе немного проспойлерила..."
    y 3q "В общем, мне очень нравится!"
    y 3n "Книга, я имею в виду!"
    y 3q "А не отрезанные конечности..."
    mc "Это как-то!.."
    "Мрачновато, не правда ли?"
    "Юри преподносила эту историю как что-то приятное, посему такой безрадостный поворот событий стал для меня большим сюрпризом."
    y 1s "А..."
    y "Не в восторге от подобного, [player]?"
    mc "Нет, дело не в этом..."
    mc "То есть, я определённо могу насладиться чем-то подобным, так что не переживай."
    y 2u "Надеюсь..."
    "Да уж... Я совсем забыл, что Юри нравится такое."
    "Она с виду такая застенчивая и замкнутая, однако в её голове всё совсем иначе."
    y "Просто такие истории..."
    y 1a "Они предлагают читателю посмотреть на мир под новым, странным углом."
    $ style.say_dialogue = style.normal
    y "Когда ужасные вещи происходят не потому, что кто-то просто хочет быть злым..."
    $ style.say_dialogue = style.edited
    y "А потому, что мир полон ужасных людей, и мы все в любом случае никчёмны."
    y "А потом, внеза-а-а-а-а-а-а-а-а-а-а-а-а-а-а-а-а-а-пно{nw}"
    $ style.say_dialogue = style.normal
    y 3v "Я... Я заговорилась, да?.."
    y "Ну вот опять..."
    y 4b "Извини..."
    mc "Эй, тебе не за что извиняться!.."
    mc "Я не потерял ни капли интереса."
    y "Ну..."
    y "Тогда ладно..."
    y 4a "Но мне показалось, что стоит рассказать тебе об этой своей привычке..."
    $ style.say_dialogue = style.normal
    y "Когда я позволяю своим мыслям заполняться вещами вроде книг и того, как они написаны..."
    $ gtext = glitchtext(24)
    $ style.say_dialogue = style.edited
    y "всё моё тело становится невероятно [gtext]{nw}"
    $ style.say_dialogue = style.normal
    $ _history_list.pop()
    y "Я забываюсь и не обращаю внимания на собеседника..."
    y 3t "Поэтому извини, если я в итоге говорю нечто странное!"
    y "И, будь добр, останавливай меня, если стану заговариваться!"
    mc "Ну, как бы..."
    mc "Я не думаю, что тебе стоит так переживать..."
    mc "Это всего лишь означает, что ты очень страстно относишься к литературе."
    mc "Слушать тебя — меньшее из того, что я могу сделать."
    mc "Да и это литературный клуб, в конце концов..."
    y 4a "Э..."
    y "Это..."
    y "Ну да, верно..."
    mc "К слову..."
    mc "Мне бы уже пора начать читать, не так ли?"
    play sound "sfx/glitch3.ogg"
    y dragon "Да-а-а-а!"
    y 3n "Ну то есть... Н-не обязательно!"
    mc "Ха-ха, ты чего?"
    y 3o "..."
    mc "Дай мне только достать книгу..."
    "Я быстро раздобыл книгу, которая лежала у меня в сумке."
    mc "Что ж... Не против, если я тут сяду?"
    "Я скользнул на место рядом с Юри."
    y 3n "Ой!.."
    y "Да..."
    mc "Точно?"
    mc "Ты выглядишь несколько напуганной..."
    y "Это..."
    y 4b "Извини..."
    y "Не то чтобы я была против!"
    y "Просто такие вещи для меня непривычны..."
    y "Читать вместе с кем-то, например."
    mc "Понятно..."
    mc "Ну, тогда скажешь, если я буду тебе мешать."
    y "Х-хорошо..."
    show yuri zorder 1 at thide
    hide yuri
    "Я открыл книгу и начал читать вступление."
    "Вскоре я понял, что Юри говорила о чтении в компании."
    "Я будто ощущал её присутствие за своим плечом, когда читал."
    "Это было не сказать, чтобы плохо."
    "Немного отвлекало, конечно, но в то же время это чувство было успокаивающим."
    "Я видел Юри краем глаза."
    "И заметил, что она не смотрит в свою книгу."
    "Я покосился на неё."
    "Похоже, он читала мою..."
    show yuri 3n zorder 2 at t11
    y "П-прости!"
    $ style.say_dialogue = style.normal
    y "Я просто!..{nw}"
    $ style.say_dialogue = style.edited
    y "Я просто{fast} купалась в жаре, что источает твоё тело т-т-т-т-т-т ело-о-о-о-о-о-о{nw}"
    $ style.say_dialogue = style.normal
    $ _history_list.pop()

    mc "Юри, ты и правда очень часто извиняешься."
    y "Я?"
    y 4a "Я это не нарочно..."
    y "Извини..."
    y 4c "То есть!.."
    mc "Ха-ха-ха."
    mc "Смотри, так будет удобнее, верно?"
    "Я придвинул свою парту впритык к парте Юри и расположил книгу посередине."
    y 2v "Ой..."
    y "Пожалуй..."
    "Юри робко закрыла свою копию книги."
    "Стоило бы нам чуть-чуть наклониться, и наши плечи бы коснулись друг друга."
    "Я почувствовал, что моя левая рука мешается, поэтому стал держать книгу правой."
    mc "Ой, теперь как-то сложновато переворачивать страницы..."
    y "Вот так..."
    $ persistent.clear[2] = True
    scene y_cg1_base with dissolve_cg
    "Юри поддержала книгу с другой стороны левой рукой, зажав её между своими большим и указательным пальцами."
    mc "О..."
    "Я повторил её действия своей правой рукой."
    "Таким образом, я переворачивал страницу, а Юри пропускала её под свой большой палец и прижимала."
    "Но при таком раскладе..."
    "Расстояние между нами уменьшилось ещё сильнее."
    "Это как бы отвлекает!.."
    "Я будто чувствовал тепло, исходившее от лица Юри: стоило мне перевести взгляд, и я бы увидел, насколько она близко..."
    show y_cg1_exp1 at cgfade
    y "Ты готов?.."
    mc "А?"
    y "Перевернуть страницу..."
    mc "А... Извини!"
    mc "Что-то на секунду отвлёкся..."
    "Я снова бросил взгляд в сторону Юри, и наши глаза встретились."
    "Я не был уверен, что смогу поспеть за ней..."
    y "Ой..."
    show y_cg1_exp2 at cgfade
    y "Ничего страшного."
    y "Ты не слишком много читаешь, верно?"
    y "Я совершенно не против подождать тебя, если ты не успеваешь..."
    y "Это, возможно, меньшее из того, что я могу сделать..."
    y "Раз уж ты был так терпелив со мной..."
    mc "А-ага..."
    mc "Спасибо."
    hide y_cg1_exp1
    hide y_cg1_exp2
    "Мы продолжили читать."
    "Юри больше не спрашивала меня, готов ли я перевернуть страницу."
    "Вместо этого я положился на то, что она заканчивала читать страницу раньше меня, и переворачивал её по своей необходимости."
    "Мы продолжали читать первую главу в тишине."
    "Несмотря на это, каждая перевёрнутая страница, казалось, была частью обмена чем-то личным..."
    "То, как мой большой палец отпускал страницу в свободное плавание на её сторону, где она ловила его своим."
    mc "Эй, Юри..."
    mc "Это может прозвучать глупо..."
    mc "Но главная героиня слегка напоминает мне тебя."
    show y_cg1_exp3 at cgfade
    y "Э-э???"
    y "Н-нет, я совершенно не могу связать себя с этим персонажем!"
    y "Ни в коем случае!"
    mc "Правда?.."
    mc "Я просто подумал о том, что она постоянно сомневается в своих словах и всё такое..."
    show y_cg1_exp1 at cgfade
    y "А-а..."
    y "Так вот ты о чём..."
    hide y_cg1_exp3
    hide y_cg1_exp1
    show y_cg1_exp2 at cgfade
    y "Извини..."
    y "Я думала, что ты... имел в виду другие её черты."
    mc "Другие черты?.."
    hide y_cg1_exp2
    show y_cg1_exp3 at cgfade
    y "Н-не важно!"
    y "Мы ещё даже толком не начали читать..."
    y "Не знаю, что на меня нашло..."
    y "Ха-ха-ха!"
    mc "Юри, ты в порядке?"
    hide y_cg1_exp3
    show y_cg1_exp1 at cgfade
    y "Э?.."
    "Юри вела себя крайне беспокойно всё время, что мы читали..."
    mc "Ты можешь отдохнуть, если плохо себя чувствуешь."
    mc "Твоё дыхание слегка..."
    y "Моё дыхание?.."
    hide y_cg1_exp1
    "Юри положила руку на свою грудь, будто пытаясь прослушать сердцебиение."
    y "Я-я и не... заметила..."
    show y_cg1_exp3 at cgfade
    y "В общем, я в порядке!"
    y "Просто мне нужно выпить воды!"
    mc "Хорошо... не перенапрягайся."
    scene bg club_day
    with dissolve_cg
    "Юри встала и практически вылетела из класса."
    mc "И что это было?.."
    show monika 1d zorder 2 at t11
    m "[player]?"
    m "Что-то случилось?"
    mc "А?"
    mc "Я без понятия..."
    mc "По-моему, Юри ведёт себя как-то странно..."
    m 1r "Значит, ты ничего не знаешь..."
    mc "Извини, получается, что так."
    mc "Ты беспокоишься за неё?"
    m 1a "Ой... да нет, не особо."
    m "Я лишь хотела убедиться, что ты ничего с ней не сделал."
    mc "Н-не, ничего такого!"
    m 5 "Ха-ха, не волнуйся... Я верю тебе, глупыш."
    m "Это нормальное для Юри поведение, не стоит тревожиться."
    mc "Ладно... как скажешь."
    m 2b "К слову, почему бы нам уже не начать обмениваться стихами?"
    mc "А?"
    mc "Разве не стоит дождаться Юри?"
    m 2a "Ну, она скорее всего задержится, так что я подумала, что можно начать и без неё."
    m "Ты не против?"
    mc "Да нет, просто спросил..."
    "Я поднялся."
    "Запомнил страницу, на которой остановился, и кинул книгу в свою сумку."
    $ y_ranaway = True
    return

label yuri_exclusive2_2:
    $ y_exclusivewatched = True
    play music t6 fadeout 1.0
    scene bg club_day
    with wipeleft_scene
    mc "Эй, Юри."
    show yuri 2f zorder 2 at t11
    y "Э?"
    mc "Оу..."
    "Я заметил, что на этот раз Юри читала совсем другую книгу."
    mc "Извини, не хотел тебе мешать..."
    y 2m "А, да нет..."
    y "Я вообще-то ждала тебя..."
    show yuri 2a
    mc "А, ну, в таком случае..."
    mc "Почему бы нам уже не начать?"
    y 2c "Да, давай!"
label yuri_exclusive2_2_ch22:
    y 3a "А вообще, у меня есть к тебе просьба..."
    y "Не против, если я сначала заварю чай?"
    mc "Конечно нет."
    y 1c "Большое спасибо."
    y 1a "Если что-то и может помочь мне скрасить время за чтением книг здесь, так это чашечка хорошего чаю."
    y "Ну и тебе, заодно."
    show yuri zorder 1 at thide
    hide yuri
    "Юри встала и направилась к кладовке."
    "Я проследовал за ней и пронаблюдал за тем, как она достала оттуда небольшой графин с сетчатым фильтром внутри."
    show yuri 1f zorder 2 at t11
    y "Можешь подержать секунду?"
    mc "Конечно..."
    "Юри отдала мне графин и достала электрический чайник."
    y "Я сейчас подключу его у учительского стола, а потом сходим за водой."
    show yuri zorder 1 at thide
    hide yuri
    "Она прошла мимо меня и поставила чайник на учительский стол."
    "Я же просто смотрел, как она двигается."
    "К моему удивлению, её движения сильно расходились с манерой речи."
    "Длинные ноги Юри особенно подчёркивали её элегантность и методичность."
    show yuri 1f zorder 2 at t11
    y "Так, можно мне теперь графин?"
    y 1a "Спасибо. Я быстро."
    mc "Я, наверное, схожу с тобой..."
    y 1q "Н-не надо!"
    y "Побудь здесь..."
    y "Я быстро."
    show yuri zorder 1 at thide
    hide yuri
    "Юри схватила графин и выскочила из класса."
    show monika 2i zorder 2 at t11
    m "О..."
    m "Юри снова тебя бросила?"
    mc "В этот раз — нет."
    mc "Она пошла налить воду в графин, чтобы сделать чай."
    m 5 "А, ладно!"
    m "Прости за непонимание..."

    scene bg club_day
    with wipeleft_scene

    "..."
    "Прошло десять минут."
    "Юри говорила, что скоро вернётся..."
    "Что могло её задержать?"
    "Мне надоело ждать, и я отправился на поиски Юри."
    scene bg corridor
    with wipeleft_scene
    $ currentpos = get_pos()
    play music "<from " + str(currentpos) + " loop 10.893>bgm/6o.ogg"
    mc "Та-ак..."
    "Первое местоположение Юри, которое приходит на ум — ближайший фонтанчик..."
    $ y_name = "Юри"
    "Я пошёл по коридору."
    $ y_name = "???"
    y "Ха-ах... ха-ах..."
    y "Ха-ах... Ха-ах..."
    "Что это был за звук?"
    "Он раздавался из-за угла..."
    "И походил на чьё-то дыхание."
    y "Ф-х-х..."
    "Резкий вдох, как будто кто-то втягивал воздух сквозь зубы."
    "Кому-то больно?.."
    "Я подошёл к углу и заглянул за него."
    mc "Юри?.."
    $ y_name = "Юри"
    show yuri cuts zorder 2 at t11
    y "У-а-а!"

    $ currentpos = 45.264 - (get_pos() / 2.0)
    $ audio.t6r = "<from " + str(currentpos) + " to 39.817 loop 0>bgm/6r.ogg"
    play music t6r
    show yuri zorder 1 at thide
    hide yuri
    show noise zorder 100 at noise_alpha
    show vignette zorder 100 at vignetteflicker(-2.030)
    show layer master at rewind
    $ y_name = "???"
    mc "{cps=*3}Юри?..{/cps}{nw}"
    "{cps=*3}Я подошёл к углу и заглянул за него.{/cps}{nw}"
    "{cps=*3}Кому-то больно?..{/cps}{nw}"
    "{cps=*3}Резкий вдох, как будто кто-то втягивал воздух сквозь зубы.{/cps}{nw}"
    y "{cps=*3}Ф-х-х...{/cps}{nw}"
    "{cps=*3}И походил на чьё-то дыхание.{/cps}{nw}"
    "{cps=*3}Он раздавался из-за угла...{/cps}{nw}"
    "{cps=*3}Что это был за звук?{/cps}{nw}"
    y "{cps=*3}Ха-ах... Ха-ах...{/cps}{nw}"
    y "{cps=*3}Ха-ах... Ха-ах...{/cps}{nw}"
    $ y_name = "Юри"
    "{cps=*3}Я пошёл по коридору.{/cps}{nw}"
    "{cps=*3}Первое местоположение Юри, которое приходит на ум — ближайший фонтанчик...{/cps}{nw}"
    mc "{cps=*3}Та-ак...{/cps}{nw}"
    window hide(None)
    window auto
    scene bg club_day
    show noise zorder 100 at noise_alpha
    show vignette zorder 100 at vignetteflicker(-2.030)
    show layer master at rewind
    "{cps=*3}Мне надоело ждать, и я отправился на поиски Юри.{/cps}{nw}"
    "{cps=*3}Что могло её задержать?{/cps}{nw}"
    "{cps=*3}Юри говорила, что скоро вернётся...{/cps}{nw}"
    "{cps=*3}Прошло десять минут.{/cps}{nw}"
    "{cps=*3}...{/cps}{nw}"

    $ del _history_list[-37:]
    if poemwinner[0] == "yuri" and chapter == 3:
        jump yuri_exclusive2_2_ch23
    $ currentpos = 90.528 - (get_pos() * 2.0)
    $ audio.t6r = "<from " + str(currentpos) + " loop 10.893>bgm/6.ogg"
    play music t6r
    hide noise
    hide vignette
    show layer master
    show yuri 1a zorder 2 at t11
    y "Я вернулась."
    y "Спасибо, что подождал."
    y "[player], тебе нравится улун?"
    mc "А, да."
    mc "Мне всё пойдёт."
    y "Как скажешь."
    "Юри установила температуру в девяносто четыре градуса."
    y 1f "Теперь нужно достать чайник."
    mc "Надо же, ты очень серьёзно к этому относишься."
    y 1u "Конечно..."
    y "Я не могу себе позволить поступать иначе, когда готовлю чай для других."
    mc "Даже несмотря на то, что я далеко не чайный эксперт?.."
    y 2m "Хе-хе."
    y 2a "В таком случае ты будешь ещё больше впечатлён."
    mc "Хм... Вполне возможно!"
    show yuri zorder 1 at thide
    hide yuri
    "Юри достала чайник и начала подбирать чайные листки."
    "К моему удивлению, она даже начала что-то напевать себе под нос."
    show yuri 1c zorder 2 at t11
    mc "Ты, должно быть, сейчас в очень хорошем настроении..."
    y 1a "Ты думаешь?"
    y "Я не скрывала этого..."
    y "И ты заметил."
    y 2u "Я тут немного подумала..."
    y "И решила, что хочу выражать свои мысли и чувства более явно."
    y "Оказалось, что мне не так уж и сложно это делать..."
    y 1c "Во всяком случае, когда рядом со мной ты."
    show yuri 1a
    mc "Эм..."
    mc "Это замечательно, Юри!"
    mc "Главное, не переутруждайся."
    y 3u "Ты всегда беспокоишься обо мне, [player]..."
    y "Это очень мило с твоей стороны."
    mc "Ну..."
    "Юри говорила правду..."
    "Я уже не знал, как мне с этим справляться!.."
    "Я наблюдал за тем, как Юри наливает нам чай."
    y 1a "[player], у меня есть ещё одна просьба."
    y "Ты не против, если мы сегодня сядем на полу?"
    mc "А? Зачем?"
    y 1h "Так легче для моей спины..."
    y "Так я смогу читать, оперевшись на стену, а не согнувшись над партой."
    mc "Оу, извини. Я даже и не подумал."
    y 1a "Ничего страшного."
    y "Просто у меня довольно часто болит спина, поэтому я стараюсь вести себя осторожнее."
    mc "Вот как?"
    mc "И почему же она у тебя болит?.."
    y 1f "Виной тому, скорее всего, моя..."
    y 1n "О-ох..."
    y 1o "М-моя..."
    mc "Твоя осанка, верно?"
    mc "Ты всё время прямо скрючиваешься, когда читаешь..."
    y 2p "Да!"
    y 2q "Во время чтения я сильно сутулюсь!"
    y "Поэтому давай лучше сядем на пол."
    mc "Договорились."
    mc "Схожу за книгой."
    show yuri zorder 1 at thide
    hide yuri
    "Я достал книгу из своей сумки."
    mc "О, у меня тут ещё есть конфеты..."
    "У меня была небольшая упаковка шоколадных конфет."
    "Я достал её, ведь шоколадные конфеты хороши к чаю."
    "Затем мы с Юри расположились на полу и поставили чашки рядом с собой."
    "Мы синхронно заняли ту же позицию, что и в прошлый раз: каждый из нас держал свою сторону книги."
    "Только вот сейчас..."
    "Наши тела были ещё ближе друг к другу."
    show yuri 2h zorder 2 at t11
    y "Что-то я не очень хорошо вижу..."
    mc "!!!"
    show yuri 2e at d11
    "Юри пододвинулась ближе, так что теперь наши плечи соприкасались."
    "И как мне теперь сконцентрироваться на чтении?!"
    "Юри всегда была довольно милой, однако..."
    "Когда её пугливость притупляется, я сам начинаю дико смущаться!"
    y 2f "Твоя чашка..."
    "Юри протянула мне чашку."
    "Я взял её в руку, которая не была занята книгой, но теперь мне было ещё сложнее сконцентрироваться."
    "Потому что теперь мне приходилось постоянно следить за тем, как бы не коснуться её груди!.."
    "Между тем Юри ни на что не обращала внимания."
    "Её лицо приняло сосредоточенное выражение, такое, будто мир вокруг неё растворился."
    "Я собрал всю свою волю в кулак, чтобы сосредоточиться на чтении."
    "..."
    "Через несколько минут я наконец-то смог немного расслабиться."
    "Поставил чашку между своих ног и попытался избавить одну из конфет от обёртки."
    mc "Ой, извини..."
    "Я ненадолго выпустил книгу из рук, чтобы доразвернуть конфету."
    mc "Можешь брать, сколько захочешь."
    y 2s "А, ну..."
    y "Да ладно, я не буду..."
    mc "Хм? Точно?"
    y 2v "Ну..."
    y "Если я возьмусь за них, то могу потом запачкать страницы..."
    mc "А, точно..."
    mc "Я об этом даже не подумал."
    mc "Виноват..."
    y 2a "Не стоит извиняться."
    y "Я подержу книгу, хорошо?"
    mc "Ты уверена?.."
    y "Конечно."
    $ persistent.clear[3] = True
    scene y_cg2_bg
    show y_cg2_base
    show y_cg2_details
    show y_cg2_nochoc
    show y_cg2_dust1
    show y_cg2_dust2
    show y_cg2_dust3
    show y_cg2_dust4
    with dissolve_cg
    "Юри открыла книгу двумя руками."
    "Она держала её так, чтобы у меня не возникло проблем с чтением."
    "Как результат — её левая рука практически лежала на моей ноге."
    mc "Ну, в таком случае..."
    "Юри снова полностью погрузилась в чтение."
    "Я достал конфету и закинул её к себе в рот."
    "Потом достал следующую..."
    "И поднёс её к Юри."
    "Она даже не повернула голову в мою сторону."
    "Лишь приоткрыла свой рот, будто данная ситуация была для неё совершенно естественной."
    "А это означало, что у меня уже нет пути назад!"
    hide y_cg2_nochoc
    "Я боязливо вложил конфету в рот Юри."
    "А она просто взяла и обхватила её своими губами."
    y "Э?.."
    show y_cg2_exp2
    "Юри внезапно изменилась в лице."
    y "Что..."
    y "Что я сейчас..."
    "Юри смотрела на меня так, будто ей было необходимо подтверждение того, что сейчас произошло."
    show y_cg2_exp3
    show y_cg2_nochoc:
        alpha 0
        linear 0.5 alpha 1
    hide y_cg2_exp2
    y "Э-эм..."
    y "[player]..."
    mc "П-прости!"
    mc "Мне, наверное, не стоило этого делать..."
    stop music
    y "А-а..."
    "Юри начала тяжело дышать."
    y "Я..."
    y "Не могу..."
    y "[player]..."
    "Юри неожиданно схватила меня за руку и вздёрнула на ноги."
    "Моя чашка перевернулась."
    scene bg closet
    show yuri 2t zorder 2 at t11
    with wipeleft
    y "[player]..."
    play sound closet_close
    show dark zorder 100
    with wipeleft
    y "Моё сердце..."
    y 2y6 "Оно стучит так сильно, [player]..."
    y "Я не могу успокоиться."
    y "Не могу больше ни на чём сосредоточиться!"
    y "Чувствуешь, [player]?"
    "Юри прижала мою руку к своей груди."
    play music hb
    show layer master at heartbeat
    y 3t "Почему это происходит со мной?"
    y "Я будто теряю рассудок..."
    y 3y4 "Я не могу с этим справиться."
    y 3y6 "Я больше не хочу читать..."
    y "Я хочу только..."
    y 3s "смотреть..."
    y "...на тебя."
    hide yuri
    show yuri eyes
    pause 3.0
    y "Ха-ах..."
    pause 3.0
    y "Ха-ах..."
    pause 3.0
    y "Ха-ах..."
    pause 3.0
    play sound closet_open
    stop music
    show layer master
    hide yuripupils
    show yuri 1n at face
    with None
    show yuri 3n at t32 with None
    hide dark
    show monika 3l zorder 3 at f31
    with wipeleft
    m "Э-эм..."
    m "Пора... обмениваться стихами..."




    return

label yuri_exclusive2_2_ch23:
    $ config.skipping = False
    scene black
    with None
    $ audio.t6g = "<loop 10.893>bgm/6g.ogg"
    play music t6g
    pause 4.62
    scene bg corridor
    show yuri eyes_base
    pause 1.0
    show bg glitch:
        yoffset 480 ytile 2
        linear 0.25 yoffset 0
        repeat
    show yuri glitch at i11
    $ gtext = glitchtext(80)
    $ currentpos = get_pos()
    play music g1
    y "[gtext]{nw}"
    stop music
    scene bg corridor
    show yuri 2n at i11
    y "Эм..."
    y "Подожди..."
    y 2o "Как я..."
    y 2y6 "Извини, у меня сейчас было очень странное чувство дежа вю..."
    y "Этого ведь не происходило ранее... да?"
    y 2t "У меня в голове в последнее время будто туманится..."
    y 3t "Надеюсь, это не особенно заметно!"
    y "Не хотела бы, чтобы ты посчитал меня странной сразу, как только мы начали проводить время вместе..."
    y "В смысле..."
    show bg corridor:
        xoffset 0
        parallel:
            0.36
            xoffset 1
            repeat
        parallel:
            0.49
            xoffset 0
            repeat
    show black zorder 5:
        alpha 0.5
        parallel:
            0.36
            alpha 0.5
            repeat
        parallel:
            0.49
            alpha 0.475
            repeat

    play music t9
    y "У каждого есть свои особенности."
    y 1v "Но показывать их сразу после знакомства с кем-то принято считать неприличным... или неприятным."
    y "Во всяком случае, таковым был мой опыт."
    y "В более юном возрасте я всегда была очень напористой, а иногда даже чересчур..."
    y "Это отталкивало от меня людей."
    y 2w "Поэтому... Я начала ненавидеть себя за всё это."
    y "За мою одержимость определёнными хобби."
    y "И за то, как я не могу сдерживать себя, когда слишком чем-то увлекаюсь."
    y "И поэтому..."
    y 1v "В какой-то момент я перестала пытаться разговаривать с людьми."
    y "Если никто не сможет полюбить меня из-за вещей, которые мне дороже всего..."
    y 1u "То проще просто оградиться от окружающих."
    y 1h "Но в последнее время что-то идёт не так."
    y "Даже не знаю, что."
    y 2y6 "Однако каждый раз, когда мы собираемся в клубе, моё сердце начинает сходить с ума."
    y "Оно будто стремится выскочить из моей груди."
    y "Оно давит на меня энергией и эмоциями, которые я не могу выпустить наружу."
    y "Оно заставляет меня делать странные вещи."
    y 2t "Я не знаю, почему так происходит!"
    stop music
    y 1t "[player]..."
    y "Мне одной кажется, что Моника в последнее время ведёт себя как-то подозрительно?"
    y 1v "С момента моего вступления она всегда была такой душкой..."
    y "Но недавно я начала чувствовать что-то колкое, когда она появляется рядом."
    y 2y4 "Я же в своём уме, правда?"
    y 2y1 "Скажи, что это так!"
    y "Раньше я молчала, потому что она постоянно подслушивала!"
    y 2y3 "Но мы наконец наедине..."
    y "Можно мы побудем так ещё немного?"
    y 1m "Ага..."
    y "..."
    play music hb
    show layer master at heartbeat
    show yuri as yuri_eyes zorder 4:
        "yuri/eyesfull.webp"
        i11
        alpha 0.0
        block:
            2.012 * 4 - 1.49
            alpha 1.0
            0.52
            alpha 0.0
            1.49
            repeat
    pause 2.0
    $ ad = 40.0
    $ ac = 1.0
    show monika 1 onlayer front at malpha(ac / ad)
    y 1s "Я просто хочу остаться."
    $ ac += 0
    show monika 1 onlayer front at malpha(ac / ad)
    y "Наедине с тобой."
    $ ac += 0
    show monika 1 onlayer front at malpha(ac / ad)
    y "Давай останемся, пока не закончится собрание."
    $ ac += 0
    show monika 1 onlayer front at malpha(ac / ad)
    y 1m "И тогда помещение клуба будет нашим."
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y "Никто не помешает нам читать."
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y 1y4 "Никто не заставит меня думать о том, чтобы полоснуть свою глотку."
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y 1q "Ха-ха-ха..."
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y "Шутка!"
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y "Всего лишь шутка."
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y 1i "Хотя... я люблю ножи..."
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y "Знаю, звучит странно, но этого не понять, пока не увидишь как прекрасны они могут быть."
    $ ac += 0.5
    show monika 1 onlayer front at malpha(ac / ad)
    y 1f "Есть идея."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "Почему бы тебе не зайти ко мне в гости?"
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1y6 "Я бы показала тебе свою коллекцию."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "Я заказывала их все у разных мастеров."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1a "Я слежу за тем, чтобы ни один из них не оставался без дела."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1m "Я ведь не хочу, чтобы они чувствовали себя одинокими..."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1y6 "Никто не заслуживает одиночества."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1y4 "Никто."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1c "Поэтому я так рада, что ты вступил в литературный клуб, [player]."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1a "Теперь нам больше не придётся быть одинокими."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "Потому что мы есть друг у друга."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "Каждый день."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "И больше ничего не нужно."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1y6 "Знаешь что?"
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "Давай бросим литературный клуб."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "Нам больше не нужно вертеться вокруг склизкого языка Моники."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1y4 "Не говоря уже про это жалкое дитя."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1s "Мы будем каждый день ходить домой вместе."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "И читать вместе."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1m "Кушать вместе."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y "Спать вместе."
    $ ac += 1
    show monika 1 onlayer front at malpha(ac / ad)
    y 1s "Разве не идеально?"
    $ ac += 2
    show monika 1 onlayer front at malpha(ac / ad)
    y "Это предел наших с тобой мечтаний."
    $ ac += 2
    show monika 1 onlayer front at malpha(ac / ad)
    y 1a "Разве не за этим ты вообще вступил в литературный клуб?"
    $ ac += 2
    show monika 1 onlayer front at malpha(ac / ad)
    y "Похоже, что была судьба."
    $ ac += 2
    show monika 1 onlayer front at malpha(ac / ad)
    y "Судьба, которая свела нас."
    $ ac += 2
    show monika 1 onlayer front at malpha(ac / ad)
    y "И теперь мы выйдем на ту счастливую концовку, что я ждала годами."
    $ ac += 2
    show monika 1 onlayer front at malpha(ac / ad)
    y "Ты будешь со мной, [player]?"
    $ ac += 2
    show monika 1 onlayer front at malpha(ac / ad)
    $ gtext = glitchtext(200)
    y "Бу{space=60}[gtext]{nw}"
    hide monika onlayer front
    window hide(None)
    $ poemsread = 0
    $ y_gave = False
    play music t5
    scene bg club_day
    window show(None)
    window auto

    return
