image sayori end-glitch:
    "sayori/end-glitch1.webp"
    0.15
    "sayori/end-glitch2.webp"
    0.15
    "sayori/end-glitch1.webp"
    0.15
    "sayori/end-glitch2.webp"
    1.00
    "sayori/end-glitch1.webp"
    0.15
    "sayori/end-glitch2.webp"
    0.15
    "sayori/end-glitch1.webp"
    0.15
    "sayori/end-glitch2.webp"

label ch40_main:
    $ s_name = "[persistent.sayoriname]"
    stop music fadeout 2.0
    scene bg residential_day
    with dissolve_scene_full

    if not persistent.monika_back:
        if persistent.monikachr == True:
            $ renpy.call_screen("dialog", message="Пожалуйста, прекрати играть с моим сердцем.\nЯ не хочу возвращаться.", ok_action=Return())
            $ persistent.monika_back = True

    $ persistent.monikachr = False
    play music t2
    "Это был обычный, ничем не примечательный школьный день."
    "Я, как всегда, был окружён влюблёнными парочками и компаниями друзей, идущими в школу вместе."
    "Я постоянно твержу себе, что пора бы уже, что ли, начинать знакомиться с девушками..."
    show sayori 1a at t11
    s "Эй, [player]..."
    "К слову, одна знакомая у меня уже есть."
    "[persistent.sayoriname], моя соседка и подруга детства."
    "До какого-то момента мы каждый день ходили в школу вместе..."
    "А недавно снова вернулись к этой привычке."
    s "[player], ты гордишься мной?"
    mc "Э? За что?"
    s 1c "Ну это..."
    s "За то, что я встала вовремя!"
    mc "Ну ты это уже довольно давно делаешь..."
    s "Вот и вот!"
    s 4h "А ты так ни разу и не сказал ничего по этому поводу!"
    show sayori at s11
    s "Несмотря на то, что мы постоянно ходим в школу вместе..."
    mc "Ну да..."
    mc "Я всегда думал, что ты и так это понимаешь."
    mc "Я стесняюсь говорить такое вслух."
    s 1d "Ну блин, пожалуйста..."
    s "Это так меня мотивирует..."
    mc "Ладно, ладно..."
    mc "Я горжусь тобой, [persistent.sayoriname]."
    show sayori at t11
    s 1q "Хе-хе-хе."
    show sayori zorder 1 at thide
    hide sayori
    "Мы перешли дорогу и направились в сторону школы."
    "Чем ближе мы к ней подходили, тем больше улицы пестрели лицами школьников, проделывающих свой ежедневный маршрут."
    show sayori 3a zorder 2 at t11
    s "Кстати, [player]..."
    s "Ты уже решил, в какой клуб вступишь?"
    mc "Клуб?"
    mc "Я ж тебе уже говорил, я совершенно не..."
    "Я хотел сказать то же, что и всегда: что я не заинтересован во вступлении ни в какой клуб."
    "Но что-то подсказывало мне, что сейчас чувства [persistent.sayoriname] будут задеты сильнее, чем обычно."
    "В конце концов, как я могу сказать ей, что клубы – это пустая трата времени..."
    "...Когда она основывает свой собственный?"
    mc "Вообще, да."
    mc "Думаю, что с клубом я определился."
    show sayori at h11
    s 1m "Правда?!"
    s 1r "И что это за клуб? Расскажи!"
    mc "Хм-м..."
    mc "Пусть это будет сюрпризом."
    s 5d "У-у-у..."
    s "Злюка."
    mc "Потерпи, скоро узнаешь."
    "Раньше я спрашивал себя, почему позволяю такой легкомысленной девчонке себя отчитывать."
    "Но теперь начал понимать, что в каком-то смысле я ей завидую..."
    "Если [persistent.sayoriname] по-настоящему захочет, она сможет добиться чего угодно."
    "Поэтому я чувствую, что должен сделать для неё что-нибудь особенное."

    scene bg class_day
    with wipeleft_scene

    "Сегодня в школе всё шло своим чередом, и не успел я оглянуться, как день уже закончился."
    "Я сложил свои вещи в сумку и поднялся из-за парты, собираясь с духом."
    mc "Ну что ж..."
    stop music fadeout 2.0

    scene bg corridor
    with wipeleft_scene
    "Я вспомнил номер кабинета, который видел на листовке клуба."
    "Прошёлся по этажу и поднялся выше — в то крыло, которое я посещал редко, поскольку в нём располагались только секции и учебные классы выпускников."
    "Через некоторое время я отыскал нужное помещение."
    "И робко открыл дверь, что находилась передо мной."
    scene bg club_day
    with wipeleft
    play music t3
    mc "Здравствуйте?.."
    show sayori 1m at t32
    s "Ой!"
    s "[player]?!"
    s 1c "Ч-что ты тут делаешь?"
    mc "Ну... Я просто..."
    "А? Я оглянул класс."
    show natsuki 3a at f31
    n "Хм."
    n "Так это ты тот [player], про которого [persistent.sayoriname] постоянно нам рассказывает?"
    show natsuki at t31
    show yuri 2t at f33
    y "С-спасибо, что заглянули!"
    y 2m "Приятно познакомиться с вами, [player]."
    y "Мы — литературный клуб."
    y 3v "Н-надеюсь, что вам у нас понравится!"
    show yuri at t33
    show natsuki at f31
    n 3g "Да лан, Юри..."
    n "К чему эти формальности?"
    n "Он ещё подумает, что у нас тут какие-нибудь строгие правила..."
    show natsuki at t31
    $ y_name = "Юри"
    $ n_name = "Нацуки"
    show yuri at f33
    y 3q "Ох..."
    y "Прости, Нацуки..."
    show yuri at t33
    "Высокая девушка, которую, по-видимому, звали Юри, казалась достаточно скромной по сравнению с остальными."
    "В противоположность ей, девушка по имени Нацуки, несмотря на свой рост, выглядела уверенной в себе."
    mc "Что ж, приятно с вами познакомиться."
    mc "С нетерпением жду нашей совместной работы."
    show sayori at f32
    s 1n "Р-работы?.."
    s 1b "[player], неужели ты..."
    s "Ты..."
    show sayori at t32
    mc "Именно."
    mc "Я решил вступить в твой клуб, [persistent.sayoriname]."
    mc "В литературный клуб."
    "У [persistent.sayoriname] загорелись глаза."
    show sayori at f32
    s 1n "Да ладно..."
    s 1s "Не может быть!"
    show sayori at hf32
    s 4s "Ура-а-а-а!"
    "[persistent.sayoriname] обхватила меня и начала прыгать."
    show sayori at t32
    mc "Э-эй..."
    show natsuki at f31
    n 3y "Хе-хе."
    n "Ну, если [persistent.sayoriname] так счастлива, то твоё присутствие здесь явно не помешает."
    show natsuki 3a at t31
    show yuri at f33
    y 1s "Не говоря уже о том, что нас теперь четверо."
    y "А это значит, что мы можем стать официально зарегистрированным клубом."
    show yuri at t33
    show sayori at f32
    s 1x "Я не знаю, что сказать!"
    s "Это нужно отметить!"
    show sayori at t32
    show yuri at f33
    y 1m "Хи-хи."
    y "И какой сегодня для этого подходящий день, да?"
    show yuri 1a at t33
    show sayori at f32
    s 1r "Ага!"
    s 1x "Ведь Нацуки решила..."
    show sayori at t32
    show natsuki at f31
    n 1w "Эй, не портьте сюрприз!"
    show natsuki at t31
    show sayori at f32
    s 5a "Хе-хе, извини..."
    show sayori at t32
    show natsuki at f31
    n 1k "Все садитесь за стол, окей?"
    show natsuki at t31
    show yuri at f33
    y 1a "Может, мне сделать чаю?"
    hide sayori
    hide natsuki
    hide yuri
    with wipeleft
    "Несколько парт были сдвинуты вместе, создавая импровизированный стол."
    "Нацуки и Юри отошли в угол класса, где первая взяла накрытый поднос, а вторая открыла кладовку."
    "До сих пор ощущая неловкость, я сел рядом с [persistent.sayoriname]."
    "Нацуки гордо прошагала к столу с подносом в руках."
    show natsuki 2z zorder 2 at t22
    n "Ну что-о-о, готовы?"
    n "Та-да-ам!"
    show sayori 4m zorder 2 at t21
    s "Ого-о-о!"
    "Нацуки сняла с подноса фольгу, и нашему взгляду открылась дюжина белых воздушных пирожных, выполненных в виде маленьких кошечек."
    "Их усы были нарисованы глазурью, а уши сделаны из маленьких кусочков шоколада."
    show sayori at f21
    s 4r "Как ми-и-ило-о-о!"
    show sayori at t21
    mc "Ого, выглядит изумительно."
    show natsuki at f22
    n 2d "Хе-хе. Ну, как бы..."
    n "Разбирайте их уже!"
    show natsuki at t22
    "[persistent.sayoriname] взяла первый. Я последовал её примеру."
    show sayori at f21
    s 4q "Вкусно!"
    show sayori at t21
    "[persistent.sayoriname] говорила с набитым ртом и даже умудрилась испачкать лицо глазурью."
    "Я крутил пирожное в руках, подбирая лучший угол для первого укуса."
    show sayori zorder 1 at thide
    hide sayori
    show natsuki 1c zorder 2 at t32
    "Нацуки молчала."
    "Я не мог не заметить, как она поглядывала в мою сторону."
    "Она что, ждёт, когда я попробую?"
    "И вот я сделал первый укус."
    "Глазурь имела сладкий, насыщенный вкус. Интересно, она приготовила её сама?"
    mc "Очень вкусно."
    mc "Спасибо, Нацуки."
    n 42c "Н-ну... Ещё бы было не вкусно!"
    n "Я же профи, в конце концов!"
    n 42a "Не нужно благодарностей..."
    show natsuki zorder 1 at thide
    hide natsuki
    "Пока Нацуки тщетно отказывалась от комплимента, Юри вернулась к столу с чайным сервизом в руках."
    "Она аккуратно расположила по чайной чашке перед каждым из нас, прежде чем поставить чайничек около подноса с пирожными."
    show yuri 1a zorder 2 at t11
    mc "Вы держите в классе целый чайный сервиз?"
    y "Не волнуйся, у нас есть разрешение от учителей."
    y "В конце концов, разве чашка горячего чая не помогает насладиться хорошей книгой?"
    mc "А... П-пожалуй..."
    show natsuki 2y at f31
    n "Хе-хе. Уже пытаешься произвести впечатление на нашего новичка, Юри?"
    show natsuki at t31
    show yuri at f11
    y 3n "Э-э?! Это не..."
    show yuri at t11
    show natsuki at thide
    hide natsuki
    "Обидевшись, Юри отвернулась."
    y 4b "Я имела в виду, ну... это..."
    mc "Верю."
    mc "Ну, чтение под чашку чая, может быть, и не для меня, но, по крайней мере, сам чай я люблю."
    y 2u "Я рада..."
    "Юри расслабилась и незаметно улыбнулась."
    y 1a "Итак, [player], что тебе нравится читать?"
    mc "Ну... Ох..."
    "Учитывая то, как мало я читал в последние годы, у меня не нашлось приемлемого ответа на этот вопрос."
    mc "Мангу..."
    "Тихо промямлил я полувсерьёз."
    show natsuki 1c zorder 2 at t41
    "Нацуки внезапно вскинула голову."
    "Она, похоже, хотела сказать что-то, но молчала."
    show natsuki zorder 1 at thide
    hide natsuki
    y 3u "Н-не слишком большой любитель читать, получается..."
    mc "Ну, это может измениться..."
    "Что я говорю?"
    "Это вырвалось само собой, стоило мне увидеть грустную улыбку Юри."
    mc "Так вот, а ты, Юри?"
    y 1l "Ну, дай-ка подумать..."
    "Юри провела пальцем по краю своей чашки."
    y 1a "Мне больше всего нравятся романы с хорошо прописанными и сложно устроенными фантастическими мирами."
    y "Я восхищаюсь уровнем креативности и авторского мастерства."
    y 1f "Равно как и способностью выстроить интересный сюжет в таком чуждом мире."
    "Юри продолжала. Было понятно, что книги — её страсть."
    "Она казалась такой сдержанной и робкой до этого момента, но теперь, по тому, как загорелись её глаза, стало очевидно, что ей уютно именно в мире книг, а не людей."
    y 2m "Ну а вообще, мне многое нравится."
    y 2a "Не пугайся того, что мало читал, хорошо?"
    y "Уверена, мы сможем найти что-то общее."
    show yuri at t22
    show natsuki 2c at f21
    n "Эй, Юри..."
    show natsuki at t21
    show yuri at f22
    y 2f "А?"
    show yuri at t22
    show natsuki at f21
    n 2h "Ну, о том... Что он сразу ответил..."
    show natsuki at t21
    mc "Манга?"
    show yuri at f22
    y 2i "Точно..."
    y "Нацуки частенько читает мангу в помещении клуба..."
    show yuri at t22
    show natsuki at f21
    n 1r "Н-ну кто тебя за язык-то тянул!"
    "Нацуки почему-то выглядела смущённой."
    n 1q "И вообще-то..."
    n "Манга... Это тоже литература, знаешь ли."
    n 1w "Так что... Если [player] захочет почитать что-нибудь из моей манги, то не пытайся остановить его!"
    show natsuki 1i at t21
    show yuri at f22
    y 1l "Нацуки..."
    y "Я бы никогда такого не сделала."
    y 1i "Однако нам бы не помешало немного разнообразить свои интересы..."
    y "Он может использовать эту возможность, чтобы попробовать что-то новое."
    y 1s "Ты согласен, [player]?"
    show yuri at t33
    show natsuki at t32
    show sayori 1l at f31
    s "В-возможно..."
    "[persistent.sayoriname] почувствовала напряжение и вмешалась."
    s 1x "Наверное, нам всем стоит попробовать что-то новое!"
    s 1l "Я думаю, это может быть весело..."
    s 1c "А ещё мы получше узнаем друг друга!"
    s 1l "Ведь..."
    s "Для этого литературные клубы и нужны... правильно?"
    show sayori at t31
    show yuri at f33
    y 1v "..."
    y "Я, к-конечно, не буду с этим спорить..."
    show yuri at t33
    show natsuki at f32
    n 2j "Ага..."
    n "Ты как всегда права, президент."
    show natsuki at t32
    show sayori at f31
    s 1q "Хе-хе-хе."
    show sayori at t31
    show natsuki at f32
    n 2c "Видимо, это означает, что мне стоит попробовать прочесть какой-нибудь роман..."
    show natsuki at t32
    mc "Ну, не только тебе..."
    mc "Я не против попробовать, если буду делать это не один."
    show sayori at thide
    hide sayori
    show natsuki at f21
    show yuri at t22
    n 2y "Что касается Юри..."
    show natsuki at t21
    show yuri at f22
    y 2n "Э?.."
    y "Мне... Мне придётся читать мангу?.."
    show yuri at t22
    show natsuki at f21
    n 4i "Блин..."
    n 4h "Ты же сама предложила нам внести разнообразие!"
    n "Тебе бы быть более открытой для новых идей..."
    n 4u "Это даже как-то обидно..."
    show natsuki at t21
    show yuri at f22
    y 2t "Обидно?.."
    y 2v "Я н-не догадывалась..."
    y "..."
    "Юри задумалась. На её лице застыло виноватое выражение."
    y 2w "Прости, что я отнеслась неуважительно к твоим интересам, Нацуки."
    y "Если... Если тебе это нравится, то я уверена, что это достойный внимания вид литературы."
    show yuri at t22
    show natsuki at f21
    n 5q "Это отговорка такая?"
    show natsuki at t21
    show yuri at f22
    y "Нет..."
    y "Я осознала свою ошибку."
    y 2t "Так что, если ты подумаешь о том, чтобы прочитать книгу..."
    y 2u "Я, в качестве ответного жеста, подберу себе какую-нибудь мангу."
    show yuri at t22
    show natsuki at f21
    n 1l "Серьёзно?!"
    n 12c "Н-ну, в смысле..."
    n "Я... рада, что ты готова пойти на это ради меня, Юри."
    n 2c "Можешь доверить поиски мне. Я обязательно найду что-нибудь, что тебе понравится, окей?"
    show natsuki at t21
    show yuri at f22
    y 1m "Я тоже..."
    y 1h "Загляну, наверное, после собрания клуба в книжный магазин."
    show yuri at t22
    show natsuki at f21
    n 1q "Ты... сама?"
    show natsuki at t21
    show yuri at f22
    y 3q "О-ой..."
    y 4a "Ты... хочешь пойти со мной?"
    show yuri at t22
    show natsuki at f21
    n 5s "Эм..."
    n "Если ты не против..."
    show natsuki at t21
    show yuri at f22
    y 3t "Ни капельки!"
    y "Я всегда хожу туда одна, так что..."
    show yuri at t22
    show natsuki at f21
    n "Да, я тоже..."
    show natsuki at t21
    show sayori 4s at l41
    s "Как это мило-о!"
    mc "[persistent.sayoriname], помолчи..."
    show sayori at lhide
    hide sayori
    show natsuki at f21
    n 2j "Я тебе там заодно покажу кое-какую мангу, хорошо?"
    show natsuki at t21
    show yuri at f22
    y 1a "Да."
    y "Жду с нетерпением."
    show natsuki at thide
    show yuri at thide
    hide natsuki
    hide yuri
    "Нацуки и Юри начали прибираться на столе."
    show sayori 1q at t11
    s "Хе-хе."
    s 1x "Что ж, вот и закончилось собрание..."
    mc "Ага, видимо..."
    mc "Хорошо, что у вас все ладят."
    s 1q "Вот и я так считаю."
    s 1d "Мне кажется, что ты всем понравился, [player]."
    mc "Думаешь?.."
    mc "Ну, в твоём присутствии, [persistent.sayoriname], все обычно ведут себя более дружелюбно."
    s 1y "У-у, [player]..."
    s "Не говори такого, ты меня смущаешь!"
    mc "Эх, ну и ладно."
    mc "Я удивился, когда ты сказала мне, что организуешь клуб..."
    mc "Но я думаю, что ты вполне себе потянешь его."
    s 1r "Мы сделаем этот клуб лучшим на свете!"
    s 1x "С твоим вступлением каждый день теперь будет таким весёлым!"
    stop music fadeout 2.0
    s 1a "Эй, [player]..."
    s "Я очень хочу поблагодарить тебя."
    s "Ну, я, конечно, очень рада, что ты вступил в наш клуб..."
    s "Но, по правде говоря, я знала, что ты сделаешь это."
    s 1q "Хе-хе."
    s 1a "Есть кое-что ещё."
    $ if all(clear for clear in persistent.clear): persistent.clearall = True
    if persistent.clearall:
        call ch40_clearall from _call_ch40_clearall
    else:
        call ch40_clearnormal from _call_ch40_clearnormal
    window hide(None)
    window auto
    $ quick_menu = False
    return

    label ch40_clearnormal:
        show sayori 1a zorder 2 at t11
        s "Я хотела поблагодарить тебя за то, что ты избавился от Моники."
        play music hb
        show black:
            alpha 0.5
            parallel:
                0.36
                alpha 0.5
                repeat
            parallel:
                0.49
                alpha 0.475
                repeat
        show layer master at heartbeat
        s 1b "Именно..."
        s "Я знаю обо всём, что она сделала."
        s 1x "Может, это потому, что теперь я президент."
        s "Но я и правда всё знаю, [player]."
        s 1q "Хе-хе."
        s 1d "Я знаю, как сильно ты старался, чтобы все были счастливы..."
        s "Я знаю обо всех гадостях, что Моника делала, чтобы никто счастлив не был..."
        s 1b "Но теперь это уже не важно."
        s "Теперь остались только мы.{nw}"
        show screen tear(20, 0.1, 0.1, 0, 40)
        play sound "sfx/s_kill_glitch1.ogg"
        pause 0.25
        stop sound
        hide screen tear
        show room_glitch zorder 1:
            xoffset -5
            0.1
            xoffset 5
            0.1
            linear 0.1 alpha 0.6
            linear 0.1 alpha 0.8
            0.1
            alpha 0
        s "Ты и я.{fast}"
        hide room_glitch
        s 1d "И ты сделал меня самой счастливой девушкой на свете."
        s "Не могу дождаться, когда смогу проводить так каждый день..."
        s "Вместе с тобой."
        play sound "sfx/s_kill_glitch1.ogg"
        show room_glitch zorder 1:
            xoffset -10
            0.1
            xoffset 0
            0.1
            linear 0.1 alpha 0.6
            linear 0.1 alpha 0.8
            0.1
            alpha 1.0
        pause 0.3
        stop sound
        s 1q "Навечно..."
        hide sayori
        show sayori 1a onlayer screens zorder 101 at face
        s "Н"
        s "а"
        s "в"
        show screen tear(20, 0.1, 0.1, 0, 40)
        play sound "sfx/s_kill_glitch1.ogg"
        pause 0.25
        stop sound
        hide screen tear
        s "е"
        s "ч"
        s "н"
        window show(None)
        stop music
        call screen dialog("Нет...", ok_action=Return())
        show layer master
        hide black
        show sayori end-glitch onlayer screens
        s "Э?"
        s "Ч-что происходит?.."
        call screen dialog("Я не позволю тебе навредить ему.", ok_action=Return())
        s "Кто..."
        s "Б-больно..."
        show screen tear(20, 0.1, 0.1, 0, 40)
        window hide(None)
        play sound "sfx/s_kill_glitch1.ogg"
        hide sayori onlayer screens
        pause 0.35
        stop sound
        hide screen tear
        window show(None)
        s "А-а-а..."
        call screen dialog("Простите... Я была неправа.", ok_action=Return())
        call screen dialog("Всё-таки здесь не найти счастья...", ok_action=Return())
        call screen dialog("Прощай, [persistent.sayoriname].", ok_action=Return())
        call screen dialog("Прощай, [player].", ok_action=Return())
        call screen dialog("Прощай, Литературный клуб.", ok_action=Return())
        $ gtext = glitchtext(120)
        s "[gtext]{nw}"
        show screen tear(20, 0.1, 0.1, 0, 40)
        window hide(None)
        play sound "sfx/s_kill_glitch1.ogg"
        pause 0.35
        stop sound
        hide screen tear
        scene black
        pause 3.0
        return

    label ch40_clearall:
        s "Я хотела поблагодарить тебя за то, что ты провёл с нами так много времени."
        play music mend
        s 2d "Ты так сильно старался сделать каждую из нас счастливой."
        s "Ты утешал нас в сложное время."
        s "Ты помогал нам поладить друг с другом."
        s 1a "Понимаешь, [player]?"
        s "Теперь, когда я стала президентом, я вижу всё."
        s 1q "Ты ведь не хотел упускать ни единой детали в этой игре, да?"
        s 1a "Ты так много раз сохранялся и загружался, лишь бы уделить время каждой из нас."
        s "Только тот, кому по-настоящему небезразличен литературный клуб, пойдёт на такие меры."
        s "Однако..."
        s 4d "Ведь именно этого я и хотела всё это время."
        s "Чтобы все были счастливы и заботились друг о друге."
        s 4q "Ха-ха..."
        s 1t "Немного грустно, не так ли?"
        s "После всего, что ты сделал для нас, я не так уж и много могу сделать в ответ."
        s "Мы дошли до конца игры."
        s 1y "Так что..."
        s "Нам пора прощаться."
        s 1d "Спасибо, что играл в {i}Doki Doki Literature Club{/i}."
        s "Я буду скучать, [player]."
        s "Заходи ещё как-нибудь, ладно?"
        s "Мы всегда будем тебе рады."
        s 1t "Мы..."
        scene black with dissolve_cg
        s "Мы все любим тебя."
        stop music fadeout 2.0
        scene black
        with Dissolve(2.0)
        return
